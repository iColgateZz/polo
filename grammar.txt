type           : baseType ( "[" "]" )* | "void" ;
baseType       : "num" | "string" | IDENTIFIER | "bool" | fnType ;
fnType         : "fn" "(" paramTypes? ")" ("->" type)? ;
paramTypes     : type ( "," type )* ;

program        : declaration* EOF ;

declaration    : funDecl
               | structDecl
               | varDecl 
               | importStmt ;

importStmt     : "import" STRING ";" ;

funDecl        : type function ;
function       : IDENTIFIER "(" parameters? ")" block ;
parameters     : type IDENTIFIER ( "," type IDENTIFIER )* ;
arguments      : expression ( "," expression )* ;

structDecl     : "struct" IDENTIFIER "{" ( type IDENTIFIER ";" )+ "}" ";" ;

varDecl        : type IDENTIFIER ( "=" expression )? ";" ;

lvalue         : IDENTIFIER ( "[" expression "]" | "." IDENTIFIER )* ;

expression     : assignment ;
assignment     : lvalue "=" assignment
               | logic_or ;
logic_or       : logic_and ( "or" logic_and )* ;
logic_and      : equality ( "and" equality )* ;
equality       : comparison ( ( "!=" | "==" ) comparison )* ;
comparison     : term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           : factor ( ( "-" | "+" ) factor )* ;
factor         : unary ( ( "/" | "*" ) unary )* ;
unary          : ( "!" | "-" ) unary | call ;
call           : primary ( "(" arguments? ")" | "." IDENTIFIER | "[" expression "]" )* ;
primary        : literal 
               | IDENTIFIER 
               | "(" expression ")"
               | arrayLiteral
               | structLiteral ;

arrayLiteral   : "{" (expression ("," expression)*)? "}" ;
structLiteral  : "{" structFields? "}" ;
structFields    : IDENTIFIER "=" expression ( "," IDENTIFIER "=" expression )* ;

statement      : assignStmt
               | exprStmt
               | forStmt
               | ifStmt
               | printStmt
               | returnStmt
               | whileStmt
               | breakStmt
               | contStmt
               | block ;

exprStmt       : expression ";" ;
forStmt        : "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" block ;
ifStmt         : "if" "(" expression ")" block ( "elif" "(" expression ")" block )* ( "else" block )? ;
printStmt      : "print" expression ";" ;
returnStmt     : "return" expression? ";" ;
whileStmt      : "while" "(" expression ")" block ;
breakStmt      : "break" ";" ;
contStmt       : "continue" ";" ;
block          : "{" (statement | varDecl)* "}" ;

literal        : NUMBER | STRING | "false" | "true" | "null" ;
NUMBER         : DIGIT+ ( "." DIGIT+ )? ;
STRING         : "\"" <any char except "\"">* "\"" ;
IDENTIFIER     : ALPHA ( ALPHA | DIGIT )* ;
ALPHA          : "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT          : "0" ... "9" ;
